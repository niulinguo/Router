plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.lingo.router'
}

router {
    wikiDir rootProject.projectDir.absolutePath
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.lingo.grade"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "size", "material", "channel"

    productFlavors {
        small {
            dimension "size"
        }
        big {
            dimension "size"
        }
        egg {
            dimension "material"
            applicationIdSuffix '.egg'
        }
        bacon {
            dimension "material"
            applicationIdSuffix '.bacon'
        }
        baidu {
            dimension "channel"
            manifestPlaceholders = [
                    MTA_CHANNEL_VALUE: "baidu"
            ]
        }
        wandoujia {
            dimension "channel"
            manifestPlaceholders = [
                    MTA_CHANNEL_VALUE: "wandoujia"
            ]
        }
        xiaomi {
            dimension "channel"
            manifestPlaceholders = [
                    MTA_CHANNEL_VALUE: "xiaomi"
            ]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (variant.buildType.name == 'release') {
                def fileName = "Router_${variant.productFlavors[0].name}_${variant.productFlavors[1].name}_${variant.productFlavors[2].name}_${defaultConfig.versionName}.apk"
                output.outputFileName = fileName
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation project(':router-annotations')
    kapt project(':router-processor')
    implementation project(':router-runtime')

    implementation project(':biz-reading')
}